apply plugin: 'java'
apply plugin: 'application'


mainClassName = "imsam.Main"

sourceCompatibility = 13
targetCompatibility = 13


// Custom configuration used to bundle libs into jar file
configurations {
    libBundle
}

repositories {
    mavenCentral()
}

dependencies {
    // Prism files not to be copied to distributions
    compileOnly files("/usr/local/prism-src/prism/classes")

    // Declare any new dependencies here using the
    // libBundle configuration
    libBundle "org.json:json:20220320"
    libBundle "args4j:args4j:2.33"
    libBundle "org.apache.logging.log4j:log4j-api:2.18.0"
    libBundle "org.apache.logging.log4j:log4j-core:2.18.0"
    libBundle "org.jblas:jblas:1.2.5"
    configurations.implementation.extendsFrom(configurations.libBundle)

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"

    // Protect against Log4j vulnerability
    constraints {
        implementation("org.apache.logging.log4j:log4j-core") {
            version {
                strictly("[2.17, 3[")
                prefer("2.17.0")
            }
            because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
        }
    }

}

jar {
    manifest {
        attributes "Main-Class": "imsam.scaffoldImportanceSampling"
        attributes "Multi-Release": true
    }
    from {
        // This bundles all the dependencies from libBundle into the jar
        configurations.libBundle.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE     // fixes duplicate error from Gradle 7
}

test {
    useJUnitPlatform()
}